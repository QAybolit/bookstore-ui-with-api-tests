plugins {
    id 'java-library'
    id 'io.qameta.allure' version '2.12.0'
    id "io.freefair.lombok" version "6.0.0-m2"
}

def junitVersion = "5.11.3",
    restAssuredVersion = "5.5.6",
    allureVersion = "2.24.0",
    aspectjVersion = "1.9.20.1",
    jacksonVersion = "2.17.2",
    slf4jVersion = "2.0.7",
    selenideVersion = "7.5.1",
    ownerVersion = "1.0.12"

repositories {
    mavenCentral()
}

allure {
    report {  // allure-commandline
        version.set(allureVersion)
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set(allureVersion) //версия интеграции фреймворка и Allure
            }
        }
    }
}

dependencies {

    testImplementation(
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "io.qameta.allure:allure-junit5:$allureVersion",
            "org.aspectj:aspectjweaver:$aspectjVersion",
            "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "com.codeborne:selenide:$selenideVersion",
            "org.aeonbits.owner:owner:$ownerVersion"
    )
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
    }
}

tasks.register("regress", Test) {

    useJUnitPlatform {
        includeTags("regress")
    }
}
